dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/ypcat.c)
AM_INIT_AUTOMAKE(yp-tools, 2.8)
AM_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Set of available languages.
ALL_LINGUAS="de"

AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

dnl Some hacks...
test "${prefix}" = "NONE" && prefix="/usr"
if test ${prefix} = '/usr'
then
dnl If we use /usr as prefix, use /usr/share/man for manual pages
	if test ${mandir} = '${prefix}/man'
        then
           mandir='${prefix}/share/man'
        fi
fi
dnl Change libexec to lib/yp
if test ${libexecdir} = '${exec_prefix}/libexec'
then
  libexecdir='${exec_prefix}/lib/yp'
fi

if test x"$ac_default_prefix" = x"/usr"
then
   rootbindir="/bin"
else
   rootbindir=$ac_default_prefix"/bin"
fi
AC_SUBST(rootbindir)

dnl Should we install domainname ? default yes
AC_ARG_ENABLE(domainname,
        [  --disable-domainname    Don't install domainname [default=yes]],
              INSTALL_DOMAINNAME=$enableval, INSTALL_DOMAINNAME=yes)
if test "$INSTALL_DOMAINNAME" = "no"
then
   DOMAINNAME=
   MAN_DOMAINNAME=
else
   DOMAINNAME=domainname
   MAN_DOMAINNAME="domainname.8 nisdomainname.8 ypdomainname.8"
fi
AC_SUBST(DOMAINNAME)
AC_SUBST(MAN_DOMAINNAME)

dnl Should we use the CRACKLIB ? default no
AC_ARG_ENABLE(cracklib,
        [  --enable-cracklib       Use the cracklib for yppasswd [default=no]],
        USE_CRACKLIB=1
        AC_DEFINE(USE_CRACKLIB,1,[Define if you want to use libcrack to check the new passwords]))

dnl Should we do extra checks even if we use the CRACKLIB ? default no
AC_ARG_ENABLE(cracklib-strict,
        [  --enable-cracklib-strict Do some extra tests before accepting [default=no]],
        USE_CRACKLIB=1;
        USE_CRACKLIB_STRICT=1;
        AC_DEFINE(USE_CRACKLIB,1,[Define if you want to use libcrack to check the new passwords])
        AC_DEFINE(USE_CRACKLIB_STRICT,1,[Define if you want to use some extra checks besides the cracklib]))

dnl Should passwd be used instead of yppasswd ? Default is no.
AC_ARG_ENABLE(call-passwd,
        [  --enable-call-passwd  yppasswd calls passwd, chfn or chsh [default=no]],
	YPPASSWD_IS_DEPRECATED=1
        AC_DEFINE(YPPASSWD_IS_DEPRECATED,1,[Define if yppasswd is deprecated and passwd should be used instead]))


dnl Checks for programs.
AC_PROG_CC
dnl If we're using gcc, we want warning flags
dnl test -n "$GCC" && WARNFLAGS="-Wall"
test -n "$GCC" && WARNFLAGS="-W -Wall -Wbad-function-cast -Wcast-align -Winline -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wshadow -Wstrict-prototypes -Wundef"
AC_SUBST(WARNFLAGS)
AC_PROG_CPP
AC_ISC_POSIX
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

if test "${YPPASSWD_IS_DEPRECATED}" = "1"; then
  AC_PATH_PROG(PASSWD_PROG, passwd)
  AC_PATH_PROG(CHFN_PROG, chfn)
  AC_PATH_PROG(CHSH_PROG, chsh)
fi

AM_WITH_DMALLOC
AM_WITH_EFENCE

dnl Checks for libraries.
dnl Replace `main' with a function in -lnsl:
AC_CHECK_LIB(nsl, yp_get_default_domain)

AC_CHECK_LIB(xcrypt,crypt,LIBCRYPT="-lxcrypt",LIBCRYPT="")
if test -z "$LIBCRYPT" ; then
  AC_CHECK_LIB(crypt,crypt,LIBCRYPT="-lcrypt",LIBCRYPT="")
  AC_CHECK_HEADERS(crypt.h)
else
  AC_CHECK_HEADERS(xcrypt.h crypt.h)
fi
AC_SUBST(LIBCRYPT)

dnl Check for libcrack
if test "${USE_CRACKLIB}" = "1"; then
  AC_CHECK_LIB(crack, FascistCheck, LIBCRACK="-lcrack",LIBCRACK="")
  AC_SUBST(LIBCRACK)
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(getopt.h rpc/clnt_soc.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNC(getopt_long, , [AC_LIBOBJ(getopt) AC_LIBOBJ(getopt1)])

AC_REPLACE_FUNCS(ypbinderr_string)

AM_FUNC_GETLINE
if test $am_cv_func_working_getline != yes; then
  AC_CHECK_FUNCS(getdelim)
else
  AC_CHECK_FUNCS(getline)
fi

dnl internationalization macros
AM_GNU_GETTEXT([external])

AC_OUTPUT([Makefile lib/Makefile src/Makefile man/Makefile m4/Makefile
	   etc/Makefile po/Makefile.in man/domainname.8 man/nicknames.5
	   man/ypcat.1 man/ypmatch.1 man/yppasswd.1 man/yppoll.8
	   man/ypset.8 man/yptest.8 man/ypwhich.1])
